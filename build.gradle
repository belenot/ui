plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish'
}

group 'com.belenot.website'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.sksamuel.hoplite:hoplite-core:1.3.13'
    implementation 'com.sksamuel.hoplite:hoplite-yaml:1.3.13'
//    implementation 'org.kodein.di:kodein-di:7.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'io.ktor:ktor-server-core:1.5.0'
    implementation 'io.ktor:ktor-server-netty:1.5.0'
    implementation 'io.ktor:ktor-jackson:1.5.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.1'

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes "Main-Class": "com.belenot.website.WebsiteKt"
    }
}

processResources.dependsOn('copyUi')

task copyUi {
    dependsOn 'ui:buildUi'

    doFirst {
        copy {
            from "ui/public/"
            into "src/main/resources/static"
        }
    }
}

shadowJar {
    classifier ''
}

publishing {
    publications {
        shadow(MavenPublication) {
            project.shadow.component(it)
        }
    }
    repositories {
        maven {
            name 'nexus'
            url snapshotsRepo // from ~/.gradle/gradle.properties
            credentials {
                username = repoUser // from ~/.gradle/gradle.properties
                password = repoPass // from ~/.gradle/gradle.properties
            }
        }
    }
}